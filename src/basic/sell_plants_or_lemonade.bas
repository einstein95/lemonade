   10 REM  SELLPLANTS -- SELL-LEMONADE
   11 REM    APPLE II ADAPTATION BY
   12 REM        JOHN COOK
   14 REM 
   15 REM        JULY, 1978
   16 REM 
   17 REM   A.P.P.L.E.  PUGETSOUND
   18 REM  PROGRAM LIBRARY EXCHANGE
   19 REM 
   20 REM     6708 39TH AVENUE SW
   21 REM     SEATTLE, WA. 98136
   22 REM 
   30 DIM PROG$(10): GOTO 2000
   50 POKE 0,PI: POKE 1,50: CALL 2: RETURN 
   60 PI=40: GOSUB 50: INPUT A$: RETURN 
   70 INPUT "TO CONTINUE, PRESS 'RETURN'",A$: RETURN 
  120 DIM A$(4),B$(3),C$(3),N$(80),A(15),E(15),I(15),N(15),P(15),Z(15): GOSUB 31983
  190 REM 
  195 N1=0:P1=0:A5=0:PR=9:S=0:I=0:N=1000:D=5:C5=3:AP=25:N9=125
  270 FOR J=0 TO 15:A(J)=0: NEXT J:J=0:E=N*C5
  310 IF B$="YES" THEN 620
  320 CALL -936: PRINT : TAB 9: PRINT "THE TOMATO PLANT STORE": PRINT 
  330 PRINT "DO YOU NEED AN INTRODUCTION TO THIS     PROGRAM? ";
  340 GOSUB 60
  350 IF A$(1,1)="N" THEN 580
  351 IF A$(1,1)="Y" THEN 360
  352 PRINT "(YES OR NO, PLEASE)": GOTO 330
  360 CALL -936: PRINT "THIS IS A COMPUTER SIMULATION OF A      BUSINESS TO SELL TOAMTO PLANTS."
  370 PRINT : PRINT "THE MAIN PURPOSE IS TO LET YOU FIND THE BEST WAY TO ADVERTISE YOUR PRODUCT."
  380 PRINT : PRINT "IMAGINE YOU ARE IN SCHOOL IN A TOWN WE  WILL CALL 'TOMATOESVILLE'."
  390 PRINT "YOUR CLASS HAS PLANTED AND RAISED ";N: PRINT "TOMATO PLANTS AS A SCIENCE PROJECT AND"
  400 PRINT "NOW YOU ARE GOING TO START A BUSINESS TOSELL THE PLANTS AND RAISE MONEY FOR YOURCLASS"
  410 PRINT "YOU HAVE ";D;" DAYS TO SELL YOUR PLANTS.": PRINT "ON THE FIRST DAY YOU SET YOUR SELLING"
  420 PRINT "PRICE AND ON EACH OF THE NEXT DAYS YOU  DECIDE HOW MANY NEW ADVERTISING SIGNS"
  425 PRINT "YOU WISH TO MAKE.": PRINT : GOSUB 70
  430 PRINT : PRINT "YOUR GOAL IS TO SELL ALL YOUR PLANTS,"
  440 PRINT "BUT STILL RAISE AS MUCH MONEY AS YOU    CAN FOR YOUR CLASS.": PRINT 
  500 PRINT "ARE YOU READY TO START? ";: GOSUB 60
  510 IF LEN(A$)=0 THEN 522: IF A$(1,1)="Y" THEN 580: IF A$(1,1)="N" THEN 530
  522 PRINT "YES OR NO, PLEASE": GOTO 500
  530 PRINT "PERHAPS YOU SHOULD ASK YOUR TEACHER FOR HELP.  IF YOU HAVE NOT RUN THE PROGRAM"
  540 PRINT "'SELLAPPLES', YOU MAY WANT TO GO BACK   AND TRY THAT PROGRAM FIRST."
  550 GOTO 1610
  570 PRINT 
  580 CALL -936: PRINT 
  590 PRINT "WHAT NAME DO YOU WISH TO GIVE YOUR      PLANT STORE?  ";
  600 INPUT N$
  610 CALL -936: PRINT 
  620 PRINT "YOU HAVE ";N;" TOMATO PLANTS TO SELL": PRINT "AND ";D;" DAYS OF SELLING TIME."
  630 PRINT : PRINT "YOUR FIXED EXPENSES FOR SEEDS,          CONTAINERS, FERTILIZERS, AND OTHER"
  640 PRINT "EXPENSES IS ";C5;" CENTS PER PLANT."
  650 PRINT "THIS MEANS THAT YOU HAVE EXPENSES BEFOREYOU START SELLING YOUR ";N;" PLANTS OF"
  670 PRINT "$";E/100;".";E MOD 100
  680 PRINT 
  690 PRINT "WHAT PRICE (IN CENTS) ARE YOU GOING TO  CHARGE FOR EACH TOMATO PLANT";
  710 INPUT P1
  720 PRINT 
  730 REM  CALCULATE THE NUMBER SOLD
  740 IF P1<PR THEN 770
  750 N1=PR ^ 2*N9/P1 ^ 2
  760 GOTO 780
  770 N1=(PR-P1)*N9*8/PR/10+N9
  780 IF J=D THEN 970
  790 FOR J=1 TO D
  800 IF J>1 THEN 860
  810 CALL -936: PRINT 
  820 PRINT "YOUR CLASS DECIDES TO ADVERTISE ON THE  FIRST DAY BY TELLING THEIR FRIENDS AND"
  830 PRINT "NOT TO SPEND ANY MONEY FOR ADVERTISING."
  835 PRINT : GOSUB 70
  840 A(J)=0
  850 GOTO 1070
  860 IF J=D THEN 870
  865 IF A5=0 THEN 920
  866 GOTO 970
  870 PRINT "DO YOU WANT TO HAVE A SALE AND LOWER    YOUR PRICE FOR THIS LAST DAY IN ORDER"
  880 PRINT "TO SELL OUT THE ";N;" PLANTS YOU HAVE": PRINT "LEFT? ";
  890 GOSUB 60
  900 IF A$(1,1)="Y" THEN 690
  910 IF A$(1,1)="N" THEN 915
  911 PRINT "YES OR NO, PLEASE";
  912 GOTO 890
  915 IF C$(1,1)="Y" THEN 950
  920 PRINT : PRINT "WOULD YOU LIKE TO TRY MAKING SOME SIGNS TO ADVERTISE YOUR BUSINESS? ";
  930 PI=30: GOSUB 50: INPUT C$
  940 IF LEN(C$)=0 THEN 942: IF C$(1,1)="N" THEN 840: IF C$(1,1)="Y" THEN 950
  942 PRINT "YES OR NO, PLEASE";: GOTO 930
  950 PRINT 
  970 CALL -936: PRINT : TAB 14: PRINT "THIS IS DAY ";J: PRINT 
  975 TAB 6: PRINT "HOW MANY ADVERTISING SIGNS": TAB 6: PRINT "DO YOU WISH TO MAKE?"
  980 A5=A5+1
  990 IF A5>2 THEN 995
  992 GOTO 1000
  995 PRINT : TAB 6: GOTO 1060
 1000 IF A5=1 THEN 1010
 1005 GOTO 1040
 1010 PRINT : TAB 6: PRINT "(EACH SIGN WILL COST YOU ": TAB 6: PRINT AP;" CENTS, BUT THEY ARE ONLY"
 1020 TAB 6: PRINT "GOOD FOR ONE DAY.)": PRINT : TAB 6
 1030 GOTO 1060
 1040 PRINT : TAB 6: PRINT "IT IS A GOOD IDEA TO TRY A": TAB 6: PRINT "DIFFERENT NUMBER OF SIGNS"
 1050 TAB 6: PRINT "EACH DAY SO YOU CAN FIND": TAB 6: PRINT "THE BEST NUMBER OF SIGNS"
 1055 TAB 6: PRINT "TO MAKE.": PRINT : TAB 6
 1060 INPUT A(J): IF A(J)>35 THEN 1072: IF A(J)>27 THEN 1075
 1070 N2=N1+N1*AD(A(J))/100
 1071 GOTO 1080
 1072 N2=2*N1: GOTO 1080
 1075 N2=N1+N1*99/100
 1080 IF N2<N THEN 1100
 1090 N2=N
 1100 I1=N2*P1
 1110 E1=A(J)*AP
 1120 I=I+I1
 1130 E=E+E1
 1140 P=I-E
 1150 N=N-N2
 1160 CALL -936: PRINT "DAY ";J;" FINANCIAL REPORT FOR": PRINT N$: PRINT 
 1170 PRINT "PRICE   ADVERTISING SIGNS   PLANTS SOLD -----   -----------------   -----------"
 1190 TAB 2: PRINT P1;: TAB 14: PRINT A(J);: TAB 33: PRINT N2
 1200 PRINT : PRINT 
 1210 PRINT "SIGN EXPENSES   INCOME   PLANTS LEFT    -------------   ------   -----------"
 1220 TAB 6: PRINT "$";E1/100;".";E1 MOD 100;: TAB 18: PRINT "$";I1/100;".";I1 MOD 100;: TAB 30: PRINT N
 1230 PRINT 
 1240 GOSUB 1740
 1250 IF J=1 THEN 1430
 1260 IF A(J)<=A(J-1) THEN 1430
 1270 PRINT "TODAY YOU MADE ";A(J)-A(J-1);" MORE SIGNS"
 1280 PRINT "THAN YESTERDAY.  THIS HELPED YOU SELL"
 1290 PRINT N(J)-N(J-1);" MORE PLANTS THAN YESTERDAY."
 1295 EX=E(J)-E(J-1)
 1300 PRINT : PRINT "THESE EXTRA SIGNS COST YOU $";EX/100;".";EX MOD 100;" MORE"
 1302 IN=I(J)-I(J-1)
 1304 IF IN>=0 THEN 1310
 1306 IN= ABS (IN)
 1308 PRINT "BUT YOUR INCOME DECREASED BY $";IN/100;".";IN MOD 100
 1309 GOTO 1340
 1310 PRINT "AND YOUR INCOME INCREASED BY $";
 1320 PRINT IN/100;".";IN MOD 100
 1340 PRINT 
 1350 GOTO 1380
 1360 FOR PI=50 TO 30 STEP -10: GOSUB 50: NEXT PI: PRINT "GOOD FIGURING, LET'S GO ON."
 1370 GOTO 1430
 1380 PRINT "DID THE EXTRA ADVERTISING YOU DID HELP?"
 1390 GOSUB 60: IF LEN(A$)#0 THEN 1400: PRINT "YES OR NO, PLEASE.  ";: GOTO 1390
 1400 IF A$(1,1)="Y" AND IN>EX THEN 1360
 1410 IF A$(1,1)="N" AND I(J)-I(J-1)<=EX THEN 1360
 1420 PRINT "YOU SHOULD LOOK AGAIN."
 1425 GOTO 1380
 1430 PRINT : GOSUB 70
 1440 IF J=(D+1)/2 THEN 1480
 1450 IF J<D THEN 1530
 1460 CALL -936: PRINT : TAB 9: PRINT "FINAL SALES REPORT FOR": TAB 5: PRINT N$
 1470 GOTO 1490
 1480 CALL -936: PRINT : TAB 7: PRINT "MID WEEK SALES REPORT FOR": TAB 5: PRINT N$
 1485 PRINT : PRINT 
 1490 PRINT "AFTER ";J;" DAY(S) OF SELLING:": PRINT 
 1500 PRINT "TOTAL";: TAB 11: PRINT "TOTAL";: TAB 30: PRINT "PLANTS"
 1505 PRINT "INCOME   EXPENSES   PROFIT    LEFT      ------   --------   ------   ------"
 1510 PRINT "$";I/100;".";I MOD 100;: TAB 12: PRINT "$";E/100;".";E MOD 100;: TAB 21
 1515 PRINT "$";P/100;".";( ABS (P)) MOD 100;: TAB 32: PRINT N
 1520 PRINT : GOSUB 70: IF J=D THEN 1525
 1522 GOTO 1530
 1525 GOSUB 1790
 1527 GOTO 1537
 1530 IF N>0 THEN 1540
 1535 D=J: GOSUB 1860
 1537 CALL -936: PRINT : GOTO 1560
 1540 CALL -936: PRINT 
 1550 NEXT J
 1560 PRINT "THANKS FOR PLAYING 'SELLPLANTS' TODAY.  I HOPE YOU LEARNED SOMETHING ABOUT HOW"
 1570 PRINT "BUSINESSES WORK.  DO MORE ADVERTISING   SIGNS MEAN 'MORE' OR 'LESS' PLANTS SOLD?"
 1590 GOSUB 60
 1600 IF A$="LESS" THEN 1605
 1602 GOTO 1620
 1605 PRINT "ARE YOU SURE?  BETTER TRY AGAIN."
 1610 GOTO 190
 1620 FOR Z=1 TO 2: FOR PI=50 TO 30 STEP -10: GOSUB 50: NEXT PI: NEXT Z
 1625 PRINT "GOOD.": PRINT : PRINT "HOW MANY SIGNS SEEMED TO BE THE BEST    FOR YOU";
 1630 INPUT S
 1640 IF S<5 OR S>20 THEN 1670
 1650 FOR PI=50 TO 40 STEP -5: GOSUB 50: NEXT PI
 1653 PRINT 
 1655 PRINT "IT LOOKS LIKE YOU HAVE THINGS FIGURED   OUT PRETTY WELL."
 1660 GOTO 1690
 1670 PRINT : PRINT "YOU SHOULD TRY AGAIN USING DIFFERENT    NUMBERS OF SIGNS AND REALLY STUDY THE"
 1680 PRINT "RESULTS TO LEARN THE BEST NUMBER OF     SIGNS."
 1690 PRINT 
 1700 PRINT "DO YOU WANT TO TRY AGAIN?";
 1710 INPUT B$
 1715 IF B$="Y" THEN 1725
 1720 GOTO 1980
 1725 CALL -936: GOTO 190
 1730 GOTO 1980
 1740 P(J)=P1
 1750 E(J)=E1
 1760 N(J)=N2
 1770 I(J)=I1
 1780 RETURN 
 1790 PRINT : IF N=0 THEN 1860
 1800 IF N<50 THEN 1840
 1810 PRINT "TOO BAD !!!  YOU STILL HAD  ";N;" PLANTS": PRINT "LEFT.  YOU LOST A LOT OF INCOME BY NOT"
 1820 PRINT "SELLING THEM."
 1830 GOTO 1870
 1840 FOR PI=70 TO 30 STEP -20: GOSUB 50: NEXT PI
 1845 PRINT "GOOD JOB.  YOU SOLD ALL BUT ";N: PRINT "OF YOUR PLANTS."
 1850 GOTO 1870
 1860 CALL -936: VTAB 15: TAB 8: PRINT "!!! CONGRATULATIONS !!!"
 1863 FOR TT=1 TO 3: FOR PI=70 TO 30 STEP -20: GOSUB 50: NEXT PI: NEXT TT
 1866 PRINT : TAB 6: PRINT "YOU SOLD ALL OF YOUR PLANTS"
 1870 PRINT : PRINT : PRINT : TAB 6: GOSUB 70: CALL -936
 1880 PRINT "TO SEE WHAT EFFECT YOUR ADVERTISING HAD ON SALES, LET'S LOOK AT A SUMMARY OF    YOUR WEEK'S SALES:"
 1900 PRINT : PRINT 
 1910 TAB 23: PRINT "PLANTS   DAY'S"
 1912 PRINT "DAY   PRICE   SIGNS    SOLD    PROFIT   ---   -----   -----   ------   ------"
 1915 FOR I=1 TO D: TAB 1: PRINT I;: IF P(I)>9 THEN 1916: TAB 9: PRINT P(I);: GOTO 1918
 1916 TAB 8: PRINT P(I);
 1918 IF A(I)>9 THEN 1920: TAB 17: PRINT A(I);: GOTO 1924
 1920 IF A(I)>99 THEN 1922: TAB 16: PRINT A(I);: GOTO 1924
 1922 TAB 15: PRINT A(I);
 1924 IF N(I)>9 THEN 1926: TAB 25: PRINT N(I);: GOTO 1930
 1926 IF N(I)>99 THEN 1928: TAB 24: PRINT N(I);: GOTO 1930
 1928 TAB 23: PRINT N(I);
 1930 Z(I)=I(I)-E(I)
 1935 IF Z(I)/100<10 AND Z(I)/100>=0 THEN 1945
 1940 TAB 32: PRINT "$";Z(I)/100;".";( ABS (Z(I))) MOD 100
 1942 GOTO 1950
 1945 TAB 33: PRINT "$";Z(I)/100;".";( ABS (Z(I))) MOD 100
 1950 NEXT I
 1960 PRINT 
 1965 GOSUB 70
 1970 RETURN 
 1980 END 
 2000 CALL -936
 2005 VTAB 5: TAB 10: PRINT "DO YOU WANT TO SELL": PRINT : TAB 14: PRINT ">> PLANTS <<": PRINT : TAB 19: PRINT "OR"
 2010 PRINT : TAB 13: PRINT ">> LEMONADE <<": PRINT : PRINT : PRINT "(SUGGESTION:"
 2012 PRINT " YOU SHOULD START WITH 'PLANTS')"
 2015 PRINT : PRINT : TAB 13: INPUT "?",PROG$
 2020 IF PROG$(1,1)="P" THEN 120
 2025 IF PROG$(1,1)="L" THEN 2040
 2030 PRINT : PRINT "PLEASE ENTER 'PLANTS' OR 'LEMONADE'": GOTO 2015
 2040 DIM A(26),L(26),G(26),H(26),B(26),S(26),P(26),Y$(6),A$(4),FN(15): GOSUB 31981: GOSUB 32000
 2150 P9=10:S3=15:S2=30:A2=200
 2200 CALL -936: PRINT : TAB 11: PRINT "THE LEMONADE STAND"
 2210 PRINT : PRINT "DO YOU NEED INSTRUCTIONS? ";
 2220 INPUT Y$
 2225 IF LEN(Y$)=0 THEN 2232
 2230 IF Y$(1,1)="N" THEN 2500
 2231 IF Y$(1,1)="Y" THEN 2240
 2232 PRINT : PRINT "YES OR NO PLEASE"
 2233 GOTO 2220
 2240 CALL -936
 2250 PRINT "IN THIS COMPUTER SIMULATION YOU WILL    MAKE DECISIONS THAT ARE SIMILAR TO"
 2260 PRINT "THOSE YOU MIGHT MAKE IN RUNNING A REAL  LEMONADE STAND.": PRINT 
 2270 PRINT "YOU HAVE AN IMAGINARY LEMONADE STAND IN A TOWN WE WILL CALL 'LEMONSVILLE, IOWA."
 2280 PRINT "YOU MAY RUN THIS SIMULATION WITH AS MANYSTANDS AS YOU WISH, HOWEVER, WHAT ONE"
 2290 PRINT "STAND DOES HAS NO EFFECT ON THE OTHER   LEMONADE STANDS."
 2310 PRINT 
 2320 PRINT "EACH DAY YOU DECIDE:"
 2325 PRINT "(1) HOW MUCH LEMONADE TO MAKE,"
 2330 PRINT "(2) HOW MANY ADVERTISING SIGNS TO MAKE      (15 CENTS EACH) AND,"
 2340 PRINT "(3) HOW MUCH TO CHARGE FOR YOUR LEMONADE"
 2350 PRINT "TRY TO MANAGE YOUR LEMONADE STAND       EFFICIENTLY SO YOU MAKE AS MUCH PROFIT  AS POSSIBLE."
 2360 PRINT : GOSUB 70
 2370 CALL -936
 2500 PRINT "ARE YOU  (1) BEGINNING A NEW SESSION                 WITH LEMONADE?"
 2501 PRINT "   OR    (2) CONTINUING AN OLD SESSION"
 2510 TAB 10: INPUT B
 2520 PRINT : PRINT "HOW MANY LEMONADE STANDS ARE THERE";
 2530 INPUT N
 2535 FOR I=1 TO N
 2536 A(I)=A2:L(I)=0:B(I)=0:S(I)=0:P(I)=0
 2537 NEXT I
 2540 IF B=1 THEN 2690
 2550 REM  INPUT VALUES FROM PREVIOUS RUN
 2560 PRINT : INPUT "LAST DAY WAS DAY NUMBER",D
 2580 FOR I=1 TO N
 2590 PRINT : PRINT "ASSETS, IN CENTS, FOR LEMONADE STAND ";I: PRINT "AFTER DAY ";D;" WERE ";
 2600 INPUT A(I)
 2610 PRINT : NEXT I
 2620 GOTO 2700
 2690 PRINT : PRINT "'ASSETS' ARE WHAT YOUR COMPANY IS WORTH.EACH STAND STARTS OUT WITH $";
 2692 PRINT A2/100;".";A2 MOD 100;" AND"
 2694 PRINT "YOU CAN NOT SPEND MORE THAN THE ASSETS  YOU HAVE AT ANY TIME."
 2696 PRINT : PRINT : GOSUB 70
 2700 PRINT : PRINT 
 2705 D=D+1
 2710 PRINT "ON DAY ";D;" YOUR COST FOR MAKING LEMONADE": PRINT "IS ";
 2720 IF D>2 THEN 2740
 2721 C=2
 2730 GOTO 2780
 2740 IF D>7 THEN 2743
 2741 C=4
 2742 GOTO 2780
 2743 C=5
 2780 PRINT C;" CENTS PER GLASS"
 2782 C1=C
 2785 R1=1:RR1=1
 2800 IF D>2 THEN 4000
 2805 FOR I=1 TO N
 2810 G(I)=1
 2820 H(I)=20
 2840 PRINT : PRINT 
 2850 TAB 12: PRINT "LEMONADE STAND ";I: PRINT 
 2860 IF B(I)=0 THEN 2890
 2870 PRINT "YOU ARE BANKRUPT, NO DECISIONS FOR YOU  TO MAKE"
 2880 GOTO 3030
 2890 PRINT "HOW MANY GLASSES OF LEMONADE DO YOU     WISH TO MAKE";
 2895 INPUT L(I)
 2900 IF L(I)=-1 THEN 5000
 2901 IF L(I)<0 OR L(I)>1000 THEN 2903
 2902 GOTO 2910
 2903 PRINT "COME ON, LET'S BE REASONABLE!  TRY AGAIN"
 2904 GOTO 2890
 2910 IF (L(I)*C1)<=A(I) THEN 2950
 2920 PRINT "THINK AGAIN!  YOU ONLY HAVE $";A(I)/100;".";: IF (A(I) MOD 100)<10 THEN PRINT "0";: PRINT A(I) MOD 100
 2930 PRINT "IN CASH AND TO MAKE ";L(I);" GLASSES OF"
 2935 PRINT "LEMONADE YOU NEED $";(L(I)*C1)/100;".";: IF ((L(I)*C1) MOD 100)<10 THEN PRINT "0";: PRINT (L(I)*C1) MOD 100
 2940 PRINT : GOTO 2890
 2950 PRINT : PRINT "HOW MANY ADVERTISING SIGNS (";S3;" CENTS"
 2951 PRINT "EACH) DO YOU WANT TO MAKE";
 2960 INPUT S(I)
 2961 IF S(I)<0 OR S(I)>50 THEN 2963
 2962 GOTO 2970
 2963 PRINT "COME ON, LET'S BE REASONABLE! TRY AGAIN"
 2964 GOTO 2950
 2970 IF (S(I)*S3)<=(A(I)-L(I)*C1) THEN 3010
 2980 PRINT : PRINT "THINK AGAIN, YOU ONLY HAVE $";(A(I)-L(I)*C1)/100;".";
 2985 IF ((A(I)-L(I)*C1) MOD 100)<10 THEN PRINT "0";: PRINT (A(I)-L(I)*C1) MOD 100;" LEFT"
 2990 PRINT "AFTER MAKING YOUR LEMONADE."
 3000 GOTO 2950
 3010 PRINT : PRINT "WHAT PRICE (IN CENTS) DO YOU WISH TO    CHARGE FOR LEMONADE ";
 3015 INPUT P(I)
 3020 IF P(I)<0 OR P(I)>100 THEN 3022
 3021 GOTO 3025
 3022 PRINT "COME ON, LET'S BE REASONABLE! TRY AGAIN"
 3023 GOTO 3010
 3025 IF C5=1 THEN 3050
 3030 NEXT I
 3050 PRINT : PRINT "ARE ALL ENTRIES CORRECT? ";
 3055 INPUT Y$
 3056 IF LEN(Y$)#0 THEN 3060
 3057 PRINT "PLEASE ANSWER 'YES' OR 'NO'."
 3058 GOTO 3050
 3060 IF Y$(1,1)="Y" THEN 3100
 3061 IF Y$(1,1)#"N" THEN 3057
 3062 C5=1
 3065 PRINT : PRINT "WHICH LEMONADE STAND SHOULD BE CHANGED?"
 3070 INPUT I
 3075 GOTO 2810
 3100 REM 
 3110 C5=0
 3120 CALL -936: PRINT 
 3130 PRINT "DAILY FINANCIAL REPORT FOR 'LEMONSVILLE'"
 3140 PRINT "DAY ";D
 3141 FOR BP=1 TO 10:PI=30: IF (2*(BP/2))#((2*BP)/2) THEN LET PI=40: GOSUB 50: NEXT BP
 3142 IF R2=2 THEN 4290
 3144 IF R3=3 THEN 4350
 3185 FOR I=1 TO N
 3187 IF R2=2 THEN 3260
 3190 IF P(I)>=P9 THEN 3220
 3200 N1=(P9-P(I))*8*S2/P9/10+S2
 3210 GOTO 3230
 3220 N1=(P9 ^ 2*S2/P(I) ^ 2)
 3230 IF S(I)>10 THEN 3235:N2=((N1+(N1*FN(S(I)))/100)/R1)*RR1: GOTO 3240
 3235 N2=2*RR1*N1/R1
 3240 N2=N2*G(I)
 3250 IF N2<=L(I) THEN 3270
 3260 N2=L(I)
 3270 M=N2*P(I)
 3280 E=S(I)*S3+L(I)*C1
 3290 P1=M-E
 3300 A(I)=A(I)+P1
 3310 IF H(I)=1 THEN 4300
 3312 IF B(I)#1 THEN 3316
 3314 PRINT : PRINT "STAND #";I;"   +++ BANKRUPT +++": GOTO 3390
 3316 PRINT : PRINT "STAND #";I
 3318 PRINT "GLASSES MADE ....";L(I)
 3320 PRINT "SIGNS ...........";S(I)
 3322 PRINT "PRICE ...........";P(I);" CENTS"
 3324 PRINT "GLASSES SOLD ....";N2
 3326 PRINT "INCOME TODAY ....$";M/100;".";: IF (M MOD 100)<10 THEN PRINT "0";: PRINT M MOD 100
 3328 PRINT "EXPENSES TODAY ..$";E/100;".";: IF (E MOD 100)<10 THEN PRINT "0";: PRINT E MOD 100
 3330 PRINT "PROFIT TODAY ....$";: IF P1<0 THEN 3332: PRINT P1/100;".";
 3331 IF (P1 MOD 100)<10 THEN PRINT "0";: PRINT P1 MOD 100: GOTO 3334
 3332 IF P1/100=0 THEN PRINT "-";: PRINT P1/100;".";: IF ( ABS (P1 MOD 100))<10 THEN PRINT "0";: PRINT ABS (P1 MOD 100)
 3334 PRINT "CURRENT ASSETS ..$";A(I)/100;".";: IF (A(I) MOD 100)<10 THEN PRINT "0";: PRINT A(I) MOD 100
 3350 IF A(I)>4 THEN 3390
 3360 PRINT "STAND ";I;" YOU DO NOT HAVE ENOUGH MONEY"
 3370 PRINT "LEFT TO STAY IN BUSINESS.               YOU ARE   +++ BANKRUPT +++"
 3380 B(I)=1
 3390 PRINT : PRINT : GOSUB 70: NEXT I
 3395 R1=1:RR1=1
 3396 R2=0
 3400 IF D#2 THEN 3450
 3410 PRINT : PRINT "YOUR MOTHERS QUIT GIVING YOU FREE SUGAR."
 3420 PRINT "YOUR COST IS NOW 4 CENTS FOR EACH GLASS YOU MAKE."
 3450 IF D#7 THEN 3480
 3460 PRINT : PRINT "INFLATION CAUSES YOUR LEMONADE MIX TO   GO UP.  YOUR COST IS NOW 5 CENTS PER    GLASS."
 3480 GOTO 2700
 4000 REM 
 4010 X= RND (6)+1
 4015 IF X>4 THEN 2805
 4020 GOTO (100*X+4000)
 4100 IF X1=1 THEN 2805
 4110 PRINT : PRINT "THERE IS A 50% CHANCE OF LIGHT RAIN     TODAY AND THE WEATHER IS COOL."
 4120 R1=2
 4130 X1=1
 4140 GOTO 2805
 4200 IF X2=1 THEN 2805
 4205 PRINT 
 4210 PRINT "THE STREET DEPARTMENT IS WORKING ON YOURSTREET SO THERE WILL BE NO TRAFFIC ON"
 4220 PRINT "YOUR STREET TODAY."
 4230 XX= RND (1): IF XX=0 THEN 4233
 4231 R2=2
 4232 GOTO 4250
 4233 R1=10
 4250 X2=1
 4260 GOTO 2805
 4290 PRINT : PRINT "THE STREET CREWS COME FOR LUNCH TO YOUR STAND AND BUY ALL THE LEMONADE YOU HAD  MADE."
 4295 GOTO 3185
 4300 IF X3=1 THEN 2805
 4310 X3=1
 4320 R3=3
 4340 GOTO 2805
 4350 PRINT : PRINT "A SEVERE THUNDERSTORM HITS YOUR TOWN    JUST AS YOU ARE SETTING UP YOUR STANDS."
 4360 PRINT "YOUR LEMONADE AND SIGNS ARE RUINED FOR  TODAY."
 4365 R3=0
 4370 FOR J=1 TO N
 4371 G(J)=0
 4372 NEXT J
 4380 GOTO 3185
 4400 IF X4=1 THEN 2805
 4410 X4=1
 4430 PRINT : PRINT "A HEAT WAVE IS PREDICTED FOR TODAY."
 4440 RR1=2
 4450 GOTO 2805
 5000 END 
31981 FN(1)=39:FN(2)=63:FN(3)=78:FN(4)=86:FN(5)=92:FN(6)=95:FN(7)=97:FN(8)=98:FN(9)=99:FN(10)=99
31982 RETURN 
31983 DIM AD(28):AD(0)=0
31985 AD(1)=14:AD(2)=26:AD(3)=36:AD(4)=45:AD(5)=53:AD(6)=59:AD(7)=65:AD(8)=69:AD(9)=74:AD(10)=78
31990 AD(11)=81:AD(12)=83:AD(13)=86:AD(14)=88:AD(15)=89:AD(16)=90:AD(17)=92:AD(18)=93:AD(19)=94:AD(20)=95
31995 AD(21)=96:AD(22)=97:AD(23)=97:AD(24)=97:AD(25)=98:AD(26)=98:AD(27)=98
32000 POKE 2,173: POKE 3,48: POKE 4,192: POKE 5,136: POKE 6,208: POKE 7,4: POKE 8,198: POKE 9,1: POKE 10,240
32005 POKE 11,8: POKE 12,202: POKE 13,208: POKE 14,246: POKE 15,166: POKE 16,0: POKE 17,76: POKE 18,2: POKE 19,0: POKE 20,96
32010 RETURN 
